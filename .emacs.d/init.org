#+TITLE: scotts emacs configuration
#+AUTHOR: Scott Stavinoha
#+PROPERTY: header-args:emacs-lisp :tangle ~/.emacs.d/init.el

* initialize package-management

#+begin_src emacs-lisp
  ;; Bootstrap straight.el
  (defvar bootstrap-version)
  (let ((bootstrap-file
         (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
        (bootstrap-version 6))
    (unless (file-exists-p bootstrap-file)
      (with-current-buffer
          (url-retrieve-synchronously
           "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
           'silent 'inhibit-cookies)
        (goto-char (point-max))
        (eval-print-last-sexp)))
    (load bootstrap-file nil 'nomessage))

  ;; Install use-package via straight
  (straight-use-package 'use-package)

  ;; Make use-package use straight by default
  (setq straight-use-package-by-default t)
  (straight-use-package 'org)
#+end_src

#+RESULTS:
: t

* navigation
#+begin_src emacs-lisp
  (global-set-key (kbd "M-o") 'other-window)
#+end_src

#+RESULTS:
: other-window

** avy
#+begin_src emacs-lisp
  (use-package avy
    :bind (("C-'" . avy-goto-char-2)
           ("M-g w" . avy-goto-word-1)
           ("M-g l" . avy-goto-line)))
#+end_src

** bookmarks
#+begin_src emacs-lisp
  (setq bookmark-save-flag 1)
#+end_src

* ui
#+begin_src emacs-lisp
  (scroll-bar-mode -1)                     ; Disable visible scrollbar
  (menu-bar-mode -1)                       ; Disable the menu bar
  (tool-bar-mode -1)                       ; Disable the toolbar

  ;; Start dired with details hidden and fit window to longest filename
  (add-hook 'dired-mode-hook #'dired-hide-details-mode)
  (setq delete-by-moving-to-trash t)

  (setq fit-window-to-buffer-horizontally t)

  (set-face-attribute 'mode-line nil :height 0.8)
  (set-face-attribute 'mode-line-inactive nil :height 0.8)

  (set-fontset-font t 'emoji "Noto Color Emoji" nil 'prepend)
  (set-fontset-font t '(#xe000 . #xf8ff) "Symbols Nerd Font Mono")
  (set-fontset-font t '(#xf0000 . #xfffff) "Symbols Nerd Font Mono")
  (set-fontset-font t '(#x100000 . #x10ffff) "Symbols Nerd Font Mono")

  (use-package nerd-icons
    ;; :custom
    ;; The Nerd Font you want to use in GUI
    ;; "Symbols Nerd Font Mono" is the default and is recommended
    ;; but you can use any other Nerd Font if you want
    ;; (nerd-icons-font-family "Symbols Nerd Font Mono")
    )

  ;; Undo/redo window layouts with C-c left/right
  (winner-mode 1)

  ;; Inline dired previews and subtree expansion
  (use-package dired-preview
    :hook (dired-mode . dired-preview-mode))

  (use-package dired-subtree
    :bind (:map dired-mode-map
                ("TAB" . dired-subtree-toggle)))

  (defun my/find-grep-dired-here ()
    "Run `find-grep-dired' against the current dired directory."
    (interactive)
    (find-grep-dired dired-directory (read-string "Grep for: ")))
  (with-eval-after-load 'dired
    (define-key dired-mode-map (kbd "M-i") #'my/find-grep-dired-here))

  ;; Pulse current line after navigation
  (use-package pulsar
    :config
    (pulsar-global-mode 1)
    (add-to-list 'pulsar-pulse-functions #'recenter-top-bottom)
    (add-to-list 'pulsar-pulse-functions #'scroll-up-command)
    (add-to-list 'pulsar-pulse-functions #'scroll-down-command)
    (add-to-list 'pulsar-pulse-functions #'other-window)
    (add-to-list 'pulsar-pulse-functions #'bookmark-jump)
    (add-to-list 'pulsar-pulse-functions #'avy-goto-char-2)
    (add-to-list 'pulsar-pulse-functions #'avy-goto-word-1)
    (add-to-list 'pulsar-pulse-functions #'avy-goto-line))

  ;; Subtle internal border padding
  (use-package spacious-padding
    :custom
    (spacious-padding-widths
     '(:internal-border-width 8
       :header-line-width 0
       :mode-line-width 0
       :tab-width 2
       :right-divider-width 8
       :fringe-width 8
       :scroll-bar-width 0))
    :config
    (spacious-padding-mode 1))
#+end_src

#+RESULTS:

** themes
#+begin_src emacs-lisp
  (load-theme 'deeper-blue t)
#+end_src

#+RESULTS:
: t

* which-key
#+begin_src emacs-lisp
  (use-package which-key
    :custom
    (which-key-idle-delay 0.5)
    :config
    (which-key-mode))
#+end_src

* backup and auto-save files
#+begin_src emacs-lisp
  ;; ============================================================
  ;; BACKUPS - snapshot of file BEFORE your edits (for "oops I saved over it")
  ;; ============================================================
  (setq make-backup-files t)
  (setq backup-directory-alist
        `(("." . ,(expand-file-name "backups/" user-emacs-directory))))

  ;; Keep multiple versions (so you can go back further)
  (setq version-control t)           ; Use version numbers on backups
  (setq kept-new-versions 5)         ; Keep 5 newest versions
  (setq kept-old-versions 2)         ; Keep 2 oldest versions
  (setq delete-old-versions t)       ; Don't ask, just delete excess backups

  ;; Backup by copying (safer, preserves file ownership/permissions)
  (setq backup-by-copying t)

  ;; ============================================================
  ;; AUTO-SAVE - periodic snapshots of UNSAVED changes (crash recovery)
  ;; ============================================================
  (setq auto-save-default t)

  ;; Put auto-save files in a central location (not in working dirs)
  (setq auto-save-file-name-transforms
        `((".*" ,(expand-file-name "auto-save/" user-emacs-directory) t)))

  ;; Create the auto-save directory if it doesn't exist
  (let ((auto-save-dir (expand-file-name "auto-save/" user-emacs-directory)))
    (unless (file-exists-p auto-save-dir)
      (make-directory auto-save-dir t)))

  ;; Also redirect the auto-save-list (tracks which files have auto-saves)
  (setq auto-save-list-file-prefix
        (expand-file-name "auto-save/.saves-" user-emacs-directory))

  ;; ============================================================
  ;; LOCK FILES (.#file) - prevent concurrent edits
  ;; ============================================================
  ;; These can't be relocated, but you can disable them if the clutter bothers you
  ;; (they're deleted as soon as you close the buffer)
  (setq create-lockfiles nil)  ; Disable if you don't edit files from multiple Emacs instances

  ;; ============================================================
  ;; Recovery tip: If Emacs crashes, run M-x recover-session
  ;; It will show you files that have auto-save data to recover
  ;; ============================================================

  (defadvice find-file (before make-directory-maybe (filename &optional wildcards) activate)
    "Create parent directory if not exists while visiting file."
    (unless (file-exists-p filename)
      (let ((dir (file-name-directory filename)))
        (unless (file-exists-p dir)
          (make-directory dir t)))))

  ;; Auto-revert buffers when files change on disk (e.g., from Dropbox sync)
  (global-auto-revert-mode 1)
  (setq auto-revert-verbose nil)     ; Don't spam messages on revert

  ;; ============================================================
  ;; SILENCE ANNOYING PROMPTS
  ;; ============================================================
  ;; Trust all dir-locals (the "apply safe/unsafe local variables?" prompt)
  (setq enable-local-variables :all)

  ;; Follow symlinks to version-controlled files without asking
  (setq vc-follow-symlinks t)
#+end_src

#+RESULTS:

* text editing
#+BEGIN_SRC emacs-lisp
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
  (delete-selection-mode 1)


  ;; Enable visual line mode globally
  (global-visual-line-mode 1)
  ;; Add some extra line spacing for better readability of wrapped lines
  (setq-default line-spacing 0.2)

  (use-package expand-region
    :bind ("C-\\" . er/expand-region))

  (use-package multiple-cursors
    :init (multiple-cursors-mode)
    :bind (("C-c m c" . mc/edit-lines)
           ("C-c m n" . mc/mark-next-like-this)
           ("C-c m s" . mc/skip-to-next-like-this)
           ("C-c m p" . mc/mark-previous-like-this)
           ("C-c m a" . mc/mark-all-like-this)))
#+END_SRC

#+RESULTS:
: mc/mark-previous-like-this

* tempel
#+begin_src emacs-lisp
  (use-package tempel
    :bind (("M-+" . tempel-insert)
           ("M-*" . tempel-complete))
    :custom
    (tempel-path (expand-file-name "~/Dropbox/org/denote/templates.eld"))
    :init
    (defun tempel-setup-capf ()
      (setq-local completion-at-point-functions
                  (cons #'tempel-complete
                        completion-at-point-functions)))
    (add-hook 'prog-mode-hook #'tempel-setup-capf)
    (add-hook 'text-mode-hook #'tempel-setup-capf))
#+end_src

* completion, search, etc
#+begin_src emacs-lisp
    ;;; completion-and-project.el --- Modern completion + project setup -*- lexical-binding: t; -*-


    ;;; Minibuffer Completion (Vertico + Orderless)
  (use-package vertico
    :ensure t
    :init (vertico-mode)
    :custom
    (vertico-cycle t)
    (vertico-count 12)
    :config
    ;; Load the directory extension (bundled with vertico)
    (require 'vertico-directory)
    :bind (:map vertico-map
                ("RET"   . vertico-directory-enter)
                ("DEL"   . vertico-directory-delete-char)
                ("M-DEL" . vertico-directory-delete-word))
    :hook (rfn-eshadow-update-overlay . vertico-directory-tidy))

  (use-package savehist
    :ensure nil  ; built-in
    :init (savehist-mode))

  (use-package orderless
    :ensure t
    :custom
    (completion-styles '(orderless basic))
    (completion-category-overrides '((file (styles basic partial-completion)))))

    ;;; In-Buffer Completion (Corfu)
  (use-package corfu
    :ensure t
    :custom
    (corfu-auto t)
    (corfu-auto-delay 0.2)
    (corfu-auto-prefix 2)
    (corfu-cycle t)
    (corfu-quit-no-match 'separator)
    :hook ((prog-mode conf-mode) . corfu-mode)
    :bind (:map corfu-map
                ("C-n" . corfu-next)
                ("C-p" . corfu-previous)
                ("C-g" . corfu-quit)))

  ;; Corfu in terminal
  (use-package corfu-terminal
    :ensure t
    :unless (display-graphic-p)
    :after corfu
    :init (corfu-terminal-mode))

    ;;; Annotations (Marginalia)
  (use-package marginalia
    :ensure t
    :init (marginalia-mode))

    ;;; Enhanced Commands (Consult)
  (use-package consult
    :ensure t
    :bind
    (;; Global bindings
     ("C-s"     . consult-line)
     ("C-x b"   . consult-buffer)
     ("C-x C-b"   . ibuffer)
     ("C-x r b" . consult-bookmark)
     ("M-y"     . consult-yank-pop)
     ("M-g g"   . consult-goto-line)
     ("M-g M-g" . consult-goto-line)
     ("M-g o"   . consult-outline)
     ("M-g i"   . consult-imenu)
     ("M-i"   . consult-ripgrep)
     ("M-s f"   . consult-fd))
    :hook (completion-list-mode . consult-preview-at-point-mode)
    :init
    (setq register-preview-delay 0.5
          register-preview-function #'consult-register-format)
    (setq consult-fd-args
  	"fd --full-path --color=never --hidden")
    (advice-add #'register-preview :override #'consult-register-window)
    :config
    (consult-customize
     consult-theme :preview-key '(:debounce 0.2 any)
     consult-ripgrep consult-git-grep consult-grep consult-fd
     consult-bookmark consult-recent-file consult-xref
     :preview-key '(:debounce 0.4 any))
    (setq consult-narrow-key "<")
    (setq consult-ripgrep-args
    	"rg --null --line-buffered --color=never --max-columns=1000 --path-separator / --smart-case --no-heading --with-filename --line-number --hidden"))

  (use-package consult-dir
    :ensure t
    :bind (("C-c ;" . consult-dir)
           :map vertico-map
           ("C-c ;" . consult-dir)
           ))

    ;;; Actions (Embark)
  (use-package embark
    :ensure t
    :bind
    (("C-."   . embark-act)
     ("C-;"   . embark-dwim)
     ("C-h B" . embark-bindings))
    :init
    (setq prefix-help-command #'embark-prefix-help-command)
    :config
    (add-to-list 'display-buffer-alist
                 '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
                   nil
                   (window-parameters (mode-line-format . none)))))

  (use-package embark-consult
    :ensure t
    :hook (embark-collect-mode . consult-preview-at-point-mode))

  (defun my-project-shell ()
    "Start an inferior shell in the current project's root directory.
    If a buffer already exists for running a shell in the project's root,
    switch to it.  Otherwise, create a new shell buffer.
    With \\[universal-argument] prefix arg, create a new inferior shell buffer even
    if one already exists."
    (interactive)
    (require 'comint)
    (let* ((default-directory (project-root (project-current t)))
           (default-project-shell-name (project-prefixed-buffer-name "shell"))
           (shell-buffer (get-buffer default-project-shell-name)))
      (if (and shell-buffer (not current-prefix-arg))
          (if (comint-check-proc shell-buffer)
              (pop-to-buffer shell-buffer (bound-and-true-p display-comint-buffer-action))
            (vterm shell-buffer))
        (vterm (generate-new-buffer-name default-project-shell-name)))))

  (advice-add 'project-shell :override #'my-project-shell)

    ;;; Project Management (project.el)
  (use-package project
    :ensure nil  ; built-in
    :bind-keymap ("C-c p" . project-prefix-map)
    :bind
    (:map project-prefix-map
          ;; Override some defaults with consult variants
          ("b" . consult-project-buffer)
          ("r" . consult-ripgrep)
          ("f" . consult-fd)
          ("v" . project-shell)
          ("a" . agent-shell))
    :config
    (setq project-list-file (expand-file-name "projects" user-emacs-directory))
    (setq project-switch-commands
          '((magit-project-status "Magit" ?m)
            (consult-fd "Find file" ?f)
            (consult-ripgrep "Ripgrep" ?r)
            (project-dired "Dired" ?d)
            (project-eshell "Eshell" ?e))))

  ;; Remember recent files for consult-recent-file
  (use-package recentf
    :ensure nil
    :init (recentf-mode)
    :custom
    (recentf-max-saved-items 200)
    (recentf-exclude '("/tmp/" "/ssh:" "COMMIT_EDITMSG")))

    ;;; Optional: Better candidate info in corfu
  (use-package cape
    :ensure t
    :init
    (add-to-list 'completion-at-point-functions #'cape-file)
    (add-to-list 'completion-at-point-functions #'cape-dabbrev))
  (use-package wgrep)
#+end_src

#+RESULTS:
* ai
** gptel
#+begin_src emacs-lisp
  (use-package gptel
      :config
      (setq gptel-default-mode 'org-mode
            gptel-use-tools 'ask
            gptel-track-media t
            gptel-model 'claude-sonnet-4-5-20250929
            gptel-backend
            (gptel-make-anthropic "Claude"
              :key (shell-command-to-string "bw get password 5322bf93-eb74-4ba6-bf47-b3950038928a"))))

  (global-set-key (kbd "C-c a g") 'gptel-send)
  (global-set-key (kbd "C-c a r") 'gptel-rewrite)

  (defun gptel-generate-code-here (prompt)
    "Generate code at point from PROMPT with file context."
    (interactive "sCode to generate: ")
    (gptel-request
      (format "File: %s\nLanguage: %s\n\nGenerate: %s"
              (or (buffer-file-name) "unknown")
              (symbol-name major-mode)
              prompt)
      :system "You are a code generator. Return ONLY the requested code. No markdown fences, no explanations, no prose. Raw code only, ready to be inserted directly into a source file."
      :position (point)
      :stream t))


  (use-package claude-code-ide
    :straight (:type git :host github :repo "manzaltu/claude-code-ide.el")
    :bind (("C-c a s" . claude-code-ide-menu))
    :custom
    (claude-code-ide-terminal-backend 'vterm)
    (claude-code-ide-window-side 'right)
    (claude-code-ide-window-width 90)
    (claude-code-ide-use-ide-diff t)
    :config
    (claude-code-ide-emacs-tools-setup))
#+end_src

#+RESULTS:
: t

*** tools
#+begin_src emacs-lisp
  ;; todo
#+end_src

*** agents
#+begin_src emacs-lisp
  (use-package gptel-agent
    :config (gptel-agent-update))

  (use-package gptel-magit
    :hook (magit-mode . gptel-magit-install))
#+end_src
*** presets
#+begin_src emacs-lisp
;; todo
#+end_src

#+RESULTS:
| :description | For quick questions. Small responses to and from minibuf. | :backend | Claude | :model | claude-3-5-haiku-20241022 | :system | You are a robot that provides information. You can search the web with the tool but only if you need to. Be as quick as possible. Limit your responses to 110 characters. | :tools | (get_file_info search_files directory_tree list_directory_with_sizes read_multiple_files read_media_file read_text_file list_directory read_file list_allowed_directories get-library-docs resolve-library-id) | :stream | t | :temperature | 1.0 | :max-tokens | nil | :use-context | user | :track-media | t | :include-reasoning | t |
** efrit
#+begin_src emacs-lisp
  (use-package efrit
    :straight (:host github :repo "steveyegge/efrit" :files ("lisp/*.el"))
    :config
    (setq efrit-model "claude-opus-4-6")
    (global-set-key (kbd "C-c a c") 'efrit-chat)
    (global-set-key (kbd "C-c a d") 'efrit-do)
    (global-set-key (kbd "C-c a a") 'efrit-agent)
    (global-set-key (kbd "C-c a p") 'efrit-do-show-progress))
#+end_src


** ai dired rename
#+begin_src emacs-lisp
  (defun my/ai-dired-rename (prompt)
    "Rename marked dired files using AI. PROMPT describes the pattern."
    (interactive "sRename pattern: ")
    (unless (derived-mode-p 'dired-mode)
      (user-error "Not in a dired buffer"))
    (let ((dir default-directory)
          (marked (dired-get-marked-files t)))
      (message "Asking AI for rename suggestions...")
      (gptel-request
        (format "Rename these files according to: %s\n\nFiles:\n%s\n\nReturn ONLY a JSON array of objects with \"old\" and \"new\" keys."
                prompt (mapconcat #'identity marked "\n"))
        :system "Return ONLY valid JSON array. No markdown fences, no explanation, no other text."
        :callback
        (lambda (response _info)
          (condition-case err
              (let* ((json-array-type 'list)
                     (json-object-type 'alist)
                     (clean (string-trim (replace-regexp-in-string "```json\\|```" "" response)))
                     (renames (json-read-from-string clean)))
                (my/ai-dired-rename--preview dir renames))
            (error (message "AI rename failed: %s" (error-message-string err))))))))

  (defun my/ai-dired-rename--preview (dir renames)
    "Show rename preview buffer for DIR with RENAMES alist."
    (let ((buf (get-buffer-create "*AI Rename Preview*")))
      (with-current-buffer buf
        (let ((inhibit-read-only t))
          (erase-buffer)
          (insert (propertize "AI Rename Preview\n" 'face 'bold))
          (insert (make-string 40 ?-) "\n\n")
          (dolist (r renames)
            (insert (format "  %s  ->  %s\n" (alist-get 'old r) (alist-get 'new r))))
          (insert "\nPress y to apply, q to cancel.\n")
          (goto-char (point-min)))
        (special-mode)
        (setq-local my/ai-rename-data (cons dir renames))
        (local-set-key (kbd "y") #'my/ai-dired-rename--apply)
        (local-set-key (kbd "q") (lambda () (interactive) (quit-window t) (message "Cancelled"))))
      (pop-to-buffer buf)))

  (defun my/ai-dired-rename--apply ()
    "Apply renames from the preview buffer."
    (interactive)
    (let* ((data my/ai-rename-data)
           (dir (car data))
           (renames (cdr data)))
      (dolist (r renames)
        (let ((old (expand-file-name (alist-get 'old r) dir))
              (new (expand-file-name (alist-get 'new r) dir)))
          (when (file-exists-p old)
            (rename-file old new t))))
      (message "Renamed %d files" (length renames))
      (quit-window t)
      (dolist (b (buffer-list))
        (with-current-buffer b
          (when (and (derived-mode-p 'dired-mode)
                     (equal default-directory dir))
            (revert-buffer))))))

  (with-eval-after-load 'dired
    (define-key dired-mode-map (kbd "C-c a") #'my/ai-dired-rename))
#+end_src

** copilot
#+begin_src emacs-lisp
  (use-package copilot
    :straight (:host github :repo "copilot-emacs/copilot.el" :files ("*.el"))
    :hook (prog-mode . copilot-mode)
    :bind (:map prog-mode-map ("<backtab>" . copilot-accept-completion)))

  (setq copilot-indent-offset-warning-disable t)



#+end_src

#+RESULTS:
: copilot-accept-completion

* git
#+begin_src emacs-lisp
  (use-package magit
    :bind ("C-x g" . magit-status)
    :custom
    (magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1))

  (use-package browse-at-remote)

  (use-package forge
    :ensure t
    :after magit)

  (use-package git-gutter
    :config (global-git-gutter-mode +1))
#+end_src

#+RESULTS:
: t

* programming
#+begin_src emacs-lisp
  (use-package treesit-auto
    :custom
    (treesit-auto-install 'prompt)  ; or t for automatic
    :config
    (treesit-auto-add-to-auto-mode-alist 'all)
    (global-treesit-auto-mode))

  (use-package eglot
    :straight nil
    :hook ((python-ts-mode . eglot-ensure)
           (js-ts-mode . eglot-ensure)
           (typescript-ts-mode . eglot-ensure)
           (rust-ts-mode . eglot-ensure)
           (go-ts-mode . eglot-ensure)))

  (use-package platformio-mode
    :ensure t
    :hook (c++-mode . platformio-conditionally-enable))
#+end_src

#+RESULTS:
| eglot-ensure |

* org
#+begin_src emacs-lisp
  (use-package org
    :straight nil
    :config
    (setq org-directory "~/Dropbox/org"
          org-agenda-files (list "~/Dropbox/org/denote/"
  			       "~/Dropbox/org/denote/journal"
                                 "~/Dropbox/org/denote/work/"
                                 "~/Dropbox/org/denote/work/journal/")
          org-clock-persist 'history
          org-clock-into-drawer t
          org-export-with-section-numbers nil
          org-hide-block-startup t
          org-src-window-setup 'current-window
          org-confirm-babel-evaluate 'my-org-confirm-babel-evaluate))

  (org-clock-persistence-insinuate)
  (global-set-key (kbd "C-c c") #'org-capture)

  ;; Org babel
  (use-package ob-http)
  (use-package ox-gfm :ensure t)

  (org-babel-do-load-languages
   'org-babel-load-languages
   '((shell . t) (emacs-lisp . t) (latex . t) (js . t) (python . t) (http . t)))

  (defun my-org-confirm-babel-evaluate (lang body)
    (not (member lang '("node" "http" "python" "emacs-lisp" "graphql" "sh" "bash" "js" "shell" "javascript" "inline-js"))))

  (add-to-list 'org-src-lang-modes '("inline-js" . javascript))
  (defvar org-babel-default-header-args:inline-js
    '((:results . "html") (:exports . "results")))
  (defun org-babel-execute:inline-js (body _params)
    (format "<script type=\"text/javascript\">\n%s\n</script>" body))

  ;; Org publishing
  (setq org-html-metadata-timestamp-format "%a %Y/%m/%d"
        org-html-prefer-user-labels t
        org-export-with-broken-links t
        org-html-postamble t
        org-html-postamble-format
        '(("en" "<p class=\"date\">Created: %d </p><p class=\"updated\">Last Updated: %C</p><p class=\"creator\">Generated by %c</p>")))

  (setq org-publish-project-alist
        '(("org-pages" :base-directory "~/Dropbox/org/roam/daily"
           :base-extension "org"
           :publishing-directory "/ssh:root@lantana.io:/var/www/html/org/daily"
           :recursive t :publishing-function org-html-publish-to-html
           :headline-levels 4 :auto-preamble t
           :exclude "daily/work/\\|iFIT\\|lantana.index.org"
           :auto-sitemap t :sitemap-filename "index.org"
           :sitemap-title "Hello" :sitemap-sort-files anti-chronologically
           :html-head-include-scripts t
           :html-head-extra "<script src=\"../../test.js\"></script>")
          ("lantana-index" :base-directory "~/Dropbox/org/"
           :base-extension "org"
           :publishing-directory "/ssh:root@lantana.io:/var/www/html/"
           :publishing-function org-html-publish-to-html)
          ("js" :base-directory "~/Dropbox/org/js"
           :base-extension "js"
           :publishing-directory "/ssh:root@lantana.io:/var/www/html/"
           :recursive t :publishing-function org-publish-attachment)
          ("org-attachments" :base-directory "~/Dropbox/org/roam/daily/data"
           :base-extension "png\\|jpg\\|gif\\|css\\|js"
           :publishing-directory "/ssh:root@lantana.io:/var/www/html/org/daily/data"
           :publishing-function org-publish-attachment :recursive t)
          ("LANtana" :components ("org-pages" "lantana-index" "org-attachments"))))

  ;; Org-download
  (use-package org-download
    :init
    (setq org-download-method 'directory
          org-download-image-dir "./_res"))

  (defun my/gptel-image-download-setup ()
    (interactive)
    (when (derived-mode-p 'org-mode)
      (with-eval-after-load 'org-download
        (setq-local org-download-image-dir
  		  (file-name-as-directory (concat (file-name-as-directory temporary-file-directory) "gptel")))
        (setq-local org-download-heading-lvl nil)
        )
      )
    )
  (add-hook 'gptel-mode-hook #'my/gptel-image-download-setup)
#+end_src

** org-capture, clock, agenda
#+begin_src emacs-lisp
  (defun my/work-journal-file ()
    "Return path to today's work journal file, creating if needed."
    (let ((denote-journal-directory (expand-file-name "~/Dropbox/org/denote/work/journal"))
          (denote-journal-file-name-title "work"))
      (denote-journal-path-to-new-or-existing-entry)))

  (setq org-capture-templates
        `(("t" "Todo" entry (file+headline "~/Dropbox/org/denote/quicknote.org" "Inbox")
           "* TODO %?\n%U\n")
          ("n" "Quick note" entry (file+headline "~/Dropbox/org/denote/quicknote.org" "Notes")
           "* %?\n%U\n")
          ("s" "Tempel snippet" plain (file "~/Dropbox/org/denote/templates.eld")
           "\n%?\n")
          ("w" "Work")
          ("wt" "Work todo" entry (file+headline my/work-journal-file "Tasks")
           "* TODO %?\n%U\n")
          ("wn" "Work note" entry (file my/work-journal-file)
           "* %?\n%U\n")))

  ;; Clock and agenda keybinds under C-c o
  (define-prefix-command 'my/org-command-map)
  (global-set-key (kbd "C-c o") 'my/org-command-map)
  (define-key my/org-command-map (kbd "c") #'org-clock-in)
  (define-key my/org-command-map (kbd "o") #'org-clock-out)
  (define-key my/org-command-map (kbd "r") #'org-clock-report)
  (define-key my/org-command-map (kbd "t") (lambda () (interactive) (org-agenda nil "p")))
  (define-key my/org-command-map (kbd "w") (lambda () (interactive) (org-agenda nil "w")))

  ;; Custom agenda views
  (setq org-agenda-custom-commands
        '(("p" "Personal TODOs"
           ((todo "TODO"
                  ((org-agenda-files (list "~/Dropbox/org/denote/"
                                           "~/Dropbox/org/denote/journal"))))))
          ("w" "Work TODOs"
           ((todo "TODO"
                  ((org-agenda-files (list "~/Dropbox/org/denote/work/"
                                           "~/Dropbox/org/denote/work/journal/"))))))))
#+end_src

** denote

#+begin_src emacs-lisp
  (use-package denote
    :straight t
    :bind (("C-c n n" . denote)              ; Create new note
           ("C-c n f" . denote-open-or-create) ; Find/create note
           ("C-c n l" . denote-link)           ; Insert link
           ("C-c n L" . denote-backlinks)      ; Show backlinks
           ("C-c n r" . denote-rename-file)    ; Rename with new title/keywords
           ("C-c n R" . denote-rename-file-using-front-matter))
    :config
    ;; Main notes directory (keeping separate from journal)
    (setq denote-directory (expand-file-name "~/Dropbox/org/denote"))

    ;; File naming
    (setq denote-file-type 'org)           ; Use org format
    (setq denote-known-keywords '("project" "ifit" "work" "personal"))
    (setq denote-infer-keywords t)         ; Suggest keywords from existing files
    (setq denote-prompts '(title keywords)) ; What to ask for when creating


    ;; Allow subdirectories
    (setq denote-allow-subdirectories t)

    ;; History
    (add-to-list 'savehist-additional-variables 'denote-history))


  ;; Work notes
  (defun my/denote-work ()
    "Create a new work note."
    (interactive)
    (let ((denote-directory (expand-file-name "~/Dropbox/org/denote/work")))
      (call-interactively #'denote)))

  (use-package denote-journal
    :straight t
    :bind (("C-c n J" . denote-journal-new-entry)     ; Today's journal
           ("C-c n j" . denote-journal-new-or-existing-entry)) ; Find journal entry
    :config
    ;; Journal lives in separate directory
    (setq denote-journal-directory (expand-file-name "~/Dropbox/org/denote/journal"))

    ;; Simpler journal file names (no signature, no keywords by default)
    (setq denote-journal-file-name-title "journal")

    ;; Daily journal (could be 'weekly or 'monthly)
    (setq denote-journal-frequency 'daily))


  (defun my/shred-quicknote ()
    "Shred quicknote.org into a denote note tagged inbox, then clear the file."
    (interactive)
    (let* ((capture-file (expand-file-name "~/Dropbox/org/denote/quicknote.org"))
           (content (with-temp-buffer
                      (insert-file-contents capture-file)
                      (string-trim (buffer-string)))))
      (if (string-empty-p content)
          (message "Capture file is empty. Nothing to shred.")
        (let ((new-file (denote "quick note" '("inbox"))))
          (with-current-buffer (find-file-noselect new-file)
            (goto-char (point-max))
            (insert content "\n")
            (save-buffer))
          ;; Clear the capture file and revert any open buffer
          (with-temp-file capture-file (insert ""))
          (when-let ((buf (find-buffer-visiting capture-file)))
            (with-current-buffer buf (revert-buffer t t)))
          (message "Shredded quicknote into %s" (file-name-nondirectory new-file))))))

  (global-set-key (kbd "C-c n q") #'my/shred-quicknote)

  ;; Optional: Work journal (separate from personal)
  ;; You can call this function or bind it
  (defun my/denote-journal-work ()
    "Create or open today's work journal entry."
    (interactive)
    (let ((denote-journal-directory (expand-file-name "~/Dropbox/org/denote/work/journal"))
          (denote-journal-file-name-title "work"))
      (denote-journal-new-or-existing-entry)))

  ;; Work prefix keymap under C-c n w
  (defun my/consult-denote-work ()
    "Find work note with consult."
    (interactive)
    (consult-find (expand-file-name "~/Dropbox/org/denote/work")))

  (define-prefix-command 'my/denote-work-map)
  (global-set-key (kbd "C-c n w") 'my/denote-work-map)
  (define-key my/denote-work-map (kbd "j") #'my/denote-journal-work)
  (define-key my/denote-work-map (kbd "n") #'my/denote-work)
  (define-key my/denote-work-map (kbd "f") #'my/consult-denote-work)

  ;; Consult integration (if you use consult)
  (with-eval-after-load 'consult
    (defun my/consult-denote ()
      "Find denote file with consult."
      (interactive)
      (consult-find denote-directory))

    (defun my/consult-denote-grep ()
      "Grep denote files with consult."
      (interactive)
      (consult-ripgrep denote-directory)))

  ;; Quick access to iFIT work service files
  (defun my/find-ifit-service ()
    "Find and open an iFIT service file from denote/work.
Shows clean service names for completion (e.g., 'user-service')."
    (interactive)
    (let* ((work-dir (expand-file-name "~/Dropbox/org/denote/work"))
           (files (directory-files work-dir t "\\.org$"))
           ;; Filter out journal files
           (service-files (seq-remove
                           (lambda (f) (string-match-p "journal" f))
                           files))
           ;; Build alist of (display-name . filepath)
           (candidates
            (mapcar
             (lambda (filepath)
               (let* ((filename (file-name-nondirectory filepath))
                      ;; Extract title part: 20230725T120000--user-service__ifit.org -> user-service
                      (title (if (string-match "--\\([^_]+\\)__" filename)
                                 (match-string 1 filename)
                               filename)))
                 (cons title filepath)))
             service-files))
           ;; Let user choose
           (chosen (completing-read "iFIT Service: " candidates nil t))
           (filepath (cdr (assoc chosen candidates))))
      (when filepath
        (find-file filepath))))

  (global-set-key (kbd "C-c n i") #'my/find-ifit-service)
#+end_src
#+RESULTS:

* document preview
#+begin_src emacs-lisp
  (use-package markdown-mode :ensure t)

  (use-package org-modern :ensure t)

  (defvar-local my/preview-active nil
    "Whether document preview mode is active in the current buffer.")

  (defun my/org-align-all-tables ()
    "Align every table in the buffer."
    (save-excursion
      (goto-char (point-min))
      (while (re-search-forward "^[ \t]*|" nil t)
        (org-table-align)
        (goto-char (org-table-end)))))

  (defun my/markdown-preview-browser ()
    "Render current markdown buffer to HTML with pandoc and open in browser."
    (let* ((input (buffer-string))
           (html-file (concat (make-temp-file "md-preview-" nil ".html"))))
      (with-temp-buffer
        (insert input)
        (call-process-region (point-min) (point-max) "pandoc"
                             t t nil
                             "-f" "gfm" "-t" "html5" "--standalone"
                             "--metadata" "title= "
                             "--css" "data:text/css;base64,Ym9keXtmb250LWZhbWlseTotYXBwbGUtc3lzdGVtLEJsaW5rTWFjU3lzdGVtRm9udCxTZWdvZSBVSSxIZWx2ZXRpY2Esc2Fucy1zZXJpZjttYXgtd2lkdGg6NTBlbTttYXJnaW46MmVtIGF1dG87cGFkZGluZzowIDFlbTtiYWNrZ3JvdW5kOiMwZDExMTc7Y29sb3I6I2MzYzhkNTtsaW5lLWhlaWdodDoxLjZ9dGFibGV7Ym9yZGVyLWNvbGxhcHNlOmNvbGxhcHNlO3dpZHRoOjEwMCV9dGgsdGR7Ym9yZGVyOjFweCBzb2xpZCAjMzA0MDUwO3BhZGRpbmc6OHB4IDEycHh9dGh7YmFja2dyb3VuZDojMTYxYjIyfXRyOm50aC1jaGlsZChldmVuKXtiYWNrZ3JvdW5kOiMxMjE3MWV9Y29kZXtiYWNrZ3JvdW5kOiMxNjFiMjI7cGFkZGluZzoycHggNnB4O2JvcmRlci1yYWRpdXM6NHB4fXByZXtiYWNrZ3JvdW5kOiMxNjFiMjI7cGFkZGluZzoxZW07Ym9yZGVyLXJhZGl1czo4cHg7b3ZlcmZsb3cteDphdXRvfWgxLGgyLGgze2NvbG9yOiNlMmU4ZjB9YXtjb2xvcjojNThhZWYyfQ==")
        (write-region (point-min) (point-max) html-file nil 'silent))
      (browse-url (concat "file://" html-file))))

  (defun my/toggle-document-preview ()
    "Toggle a rendered/styled preview for org and markdown buffers."
    (interactive)
    (cond
     ((derived-mode-p 'org-mode)
      (if my/preview-active
          (progn
            (org-modern-mode -1)
            (org-indent-mode -1)
            (org-toggle-inline-images)
            (setq my/preview-active nil)
            (message "Org preview off"))
        (my/org-align-all-tables)
        (org-modern-mode 1)
        (org-indent-mode 1)
        (org-toggle-inline-images)
        (setq my/preview-active t)
        (message "Org preview on")))
     ((derived-mode-p 'markdown-mode)
      (my/markdown-preview-browser)
      (message "Markdown preview opened in browser"))
     (t (message "Not an org or markdown buffer"))))

  (global-set-key (kbd "C-c t p") #'my/toggle-document-preview)
#+end_src

* tmr
#+begin_src emacs-lisp
  (use-package tmr
    :bind (("C-c k t" . tmr)
           ("C-c k l" . tmr-tabulated-view)
           ("C-c k c" . tmr-cancel)))
#+end_src

* terminal
#+begin_src emacs-lisp
  (use-package vterm)

(use-package openwith
  :config
  (setq openwith-associations '(("\\.\\(mkv\\|mp4\\|avi\\|webm\\)\\'" "mpv" (file))))
  (openwith-mode t))

(setq large-file-warning-threshold nil)
#+end_src
* email
#+begin_src emacs-lisp
  (org-babel-load-file (expand-file-name "email.org" user-emacs-directory))
#+end_src

* /Local Variables/
# Local Variables:
# eval: (add-hook 'after-save-hook (lambda () (org-babel-tangle)) nil t)
# End:
